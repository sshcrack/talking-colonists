import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java-library'
    id 'net.neoforged.moddev' version '2.0.88'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'idea'
    id "com.modrinth.minotaur" version "2+"
    id 'net.darkhax.curseforgegradle' version '1.1+'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()


    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }


    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }

    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            getGameDirectory().convention(project.getLayout().getProjectDirectory().dir("run_server"));
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}


dependencies {
    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
    localRuntime "maven.modrinth:simple-voice-chat:neoforge-${voicechat_dependency}"

    localRuntime "curse.maven:structurize-298744:6515153"
    localRuntime "curse.maven:multi-piston-303278:6515286"
    localRuntime "curse.maven:blockui-522992:6367809"
    localRuntime "curse.maven:domum-ornamentum-527361:6335398"

    implementation "curse.maven:minecolonies-245506:${minecolonies_file}"
    jarJar(implementation('org.java-websocket:Java-WebSocket') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }) {
        version {
            strictly '[1.5,)'
        }
    }
    additionalRuntimeClasspath "org.java-websocket:Java-WebSocket:1.6.0"
}


var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            voicechat_dependency   : voicechat_dependency,
            minecolonies_version   : minecolonies_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}


def localProperties = new Properties()
def localPropertiesFile = new File(rootProject.projectDir, "local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def modrinth_key = localProperties.getProperty('MODRINTH_API_KEY') ?: ""
def curse_key = localProperties.getProperty('CURSEFORGE_API_KEY') ?: ""


// Place modrinth block at the very end to ensure all properties are loaded and plugin is applied
modrinth {
    token = modrinth_key
    projectId = 'QJvWFsra'
    uploadFile = jar.archiveFile
    versionType = "beta"
    loaders = ["neoforge"]
    gameVersions = [minecraft_version]
    syncBodyFrom = rootProject.file("README.md").text
    dependencies {
        required.project "simple-voice-chat"
    }
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = curse_key

    def mainFile = upload(curseforge_project, jar)
    mainFile.addModLoader("neoforge")
    mainFile.releaseType = "beta"
    mainFile.addEnvironment("client", "server")
    mainFile.addGameVersion(minecraft_version)
    mainFile.addRequirement("simple-voice-chat")
    mainFile.addRequirement("minecolonies")
    mainFile.changelog = "For a changelog take a look at https://github.com/sshcrack/talking-colonists/"
}

tasks.register("publishAll")

tasks.publishAll.dependsOn(tasks.modrinth)
tasks.publishAll.dependsOn(tasks.publishCurseForge)
